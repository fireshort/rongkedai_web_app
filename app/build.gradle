buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
    }
}
apply plugin: 'com.android.application'

repositories {
    jcenter()
}

android {
    compileSdkVersion 19
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.rongkedai"
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 6
        versionName "0.92"
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def fileName = outputFile.name
                        .replace(".apk", "-${variant.versionName}(${variant.versionCode}).apk")
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }

    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    dexOptions {
        preDexLibraries = false
    }

    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

assembleRelease << {
    print "Copying APK to root directory..."
    copy {
        from 'build/outputs/apk'
        into project.rootDir
        exclude '*unaligned*', '*debug*'
        rename {
            String fileName -> fileName.replace('app', rootProject.name)
        }
    }
    println "done"
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])    
    compile 'com.android.support:support-v4:21.0.3'
    compile 'com.android.support:appcompat-v7:20.0.0'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'in.srain.cube:ultra-ptr:1.0.7@aar'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.3'
    compile 'com.github.chrisbanes.photoview:library:1.2.3'
}
